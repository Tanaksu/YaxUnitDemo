#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.ДобавитьТестовыйНабор("Создание документов товародвижения").ВТранзакции()
			.ДобавитьТест("СозданиеДокументов")
		.ДобавитьТестовыйНабор("Проведение документов товародвижения").ВТранзакции()		
			.ДобавитьТест("ПроведениеДокументовПозитив")
			.ДобавитьТест("ПроведениеДокументовНегатив")	
		.ДобавитьТестовыйНабор("КонтрольДвижений").ВТранзакции()
			.ДобавитьТест("КонтрольДвиженийПозитив")
			.ДобавитьТест("КонтрольДвиженийНегатив")
				
	;
	
КонецПроцедуры

Процедура СозданиеДокументов() Экспорт
	ТестТовар1 = СозданиеТовара("ТестМонитор");
	ТестТовар2 = СозданиеТовара("ТестКлавиатура");
	ТестСклад1 = СозданиеСклада("ТестОсновной");
	
	//Создание документов Приход товара
	НовыйПриходТовара = СозданиеПриходаТовара(ТестТовар1, ТестСклад1, 10, 1000);
	
	ЮТест.ОжидаетЧто(НовыйПриходТовара)
		.ИмеетТип("ДокументСсылка.ПриходТовара")
		;
		
	НовыйПриходТовара = СозданиеПриходаТовара(ТестТовар2, ТестСклад1, 20, 300);	
	
	ЮТест.ОжидаетЧто(НовыйПриходТовара)
		.ИмеетТип("ДокументСсылка.ПриходТовара")
		;
		
	//Создание документов Расход товара
	НовыйРасходТовара = СозданиеРасходаТовара(ТестТовар1, ТестСклад1, 10, 2000);
	
	ЮТест.ОжидаетЧто(НовыйРасходТовара)
		.ИмеетТип("ДокументСсылка.РасходТовара")
		;
		
	НовыйРасходТовара = СозданиеРасходаТовара(ТестТовар1, ТестСклад1, 5, 2000);
	
	ЮТест.ОжидаетЧто(НовыйРасходТовара)
		.ИмеетТип("ДокументСсылка.РасходТовара")
		;	
		
	ТестСклад2 = СозданиеСклада("ТестМагазин");
	
	НовыйРасходТовара = СозданиеРасходаТовара(ТестТовар2, ТестСклад2, 15, 700);
	
	ЮТест.ОжидаетЧто(НовыйРасходТовара)
		.ИмеетТип("ДокументСсылка.РасходТовара")
		;			
		
КонецПроцедуры

Процедура ПроведениеДокументовПозитив() Экспорт

	ТестТовар1 = СозданиеТовара("ТестМонитор");
	ТестСклад1 = СозданиеСклада("ТестОсновной");
	
	//Приход товара
	НовыйПриходТовара = СозданиеПриходаТовара(ТестТовар1, ТестСклад1, 20, 1000);
	ЮТест.ОжидаетЧто(НовыйПриходТовара.ПолучитьОбъект())
		.Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)
		.НеВыбрасываетИсключение()
		;
	
	Движения = ЮТЗапросы.ДвиженияДокумента(НовыйПриходТовара, "ТоварныеЗапасы");
		
	ЮТест.ОжидаетЧто(Движения)	
		.Заполнено()
		;
	
	//Расход товара	
	НовыйРасходТовара = СозданиеРасходаТовара(ТестТовар1, ТестСклад1, 10, 2000);
	ЮТест.ОжидаетЧто(НовыйРасходТовара.ПолучитьОбъект())
		.Метод("Записать", ЮтМетоды.МассивПараметров(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный))
		.НеВыбрасываетИсключение()
		;
	
	Движения = ЮТЗапросы.ДвиженияДокумента(НовыйРасходТовара, "ТоварныеЗапасы");
	ЮТест.ОжидаетЧто(Движения)	
		.Заполнено()
		;	
			
КонецПроцедуры

Процедура ПроведениеДокументовНегатив() Экспорт
	
	ТестТовар1 = СозданиеТовара("ТестМонитор");
	ТестСклад1 = СозданиеСклада("ТестМагазин");
	
	//Расход товара	
	НовыйРасходТовара = СозданиеРасходаТовара(ТестТовар1, ТестСклад1, 30000, 2000);
	ЮТест.ОжидаетЧто(НовыйРасходТовара.ПолучитьОбъект())
		.Метод("Записать", ЮтМетоды.МассивПараметров(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный))
		.ВыбрасываетИсключение("Не удалось провести ""Продажа")
	;	
	
КонецПроцедуры

Процедура КонтрольДвиженийПозитив() Экспорт
	Валюта = Константы.ВалютаУчета.Получить();
	
	ТестТовар	   = СозданиеТовара("ТестКлавиатура");
	ТестСклад	   = СозданиеСклада("ТестОсновной");
	ТестПокупатель = СозданиеКонтрагента("ТестПокупатель");
	
	ТестПоступлениеДенег = СозданиеПоступленияДенег(ТестПокупатель, 10000);
	ТестПриходТовара	 = СозданиеПриходаТовара(ТестТовар, ТестСклад, 10, 500);
	ТестПриходТовара.ПолучитьОбъект().Записать(РежимЗаписиДокумента.Проведение);
	
	ТестРасходТовара = СозданиеРасходаТовара(ТестТовар, ТестСклад, 10, 1000);
	ОбРасходТовара = ТестРасходТовара.ПолучитьОбъект();
	ОбРасходТовара.Покупатель = ТестПокупатель;
	ОбРасходТовара.Валюта = Валюта;
	ОбРасходТовара.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
	
	ОписаниеЗапроса = ЮТЗапросы.ОписаниеЗапроса();
	ОписаниеЗапроса.ИмяТаблицы = "РегистрНакопления.ТоварныеЗапасы.Остатки";
	ОписаниеЗапроса.Условия.Добавить("Товар = &Товар");
	ОписаниеЗапроса.Условия.Добавить("Склад = &Склад");
	ОписаниеЗапроса.ЗначенияПараметров.Вставить("Товар", ТестТовар);
	ОписаниеЗапроса.ЗначенияПараметров.Вставить("Склад", ТестСклад);
	ОписаниеЗапроса.ВыбираемыеПоля.Добавить("КоличествоОстаток");
	
	ЮТест.ОжидаетЧто(ЮТЗапросы.РезультатПустой(ОписаниеЗапроса)).ЭтоИстина();
	
	ОписаниеЗапроса = ЮТЗапросы.ОписаниеЗапроса();
	ОписаниеЗапроса.ИмяТаблицы = "РегистрНакопления.Взаиморасчеты.Остатки";
	ОписаниеЗапроса.Условия.Добавить("Контрагент = &Контрагент");
	ОписаниеЗапроса.Условия.Добавить("Валюта = &Валюта");
	ОписаниеЗапроса.ЗначенияПараметров.Вставить("Контрагент", ТестПокупатель);
	ОписаниеЗапроса.ЗначенияПараметров.Вставить("Валюта", Валюта);
	ОписаниеЗапроса.ВыбираемыеПоля.Добавить("СуммаОстаток");
	
	ЮТест.ОжидаетЧто(ЮТЗапросы.РезультатПустой(ОписаниеЗапроса)).ЭтоИстина();
	
КонецПроцедуры

Процедура КонтрольДвиженийНегатив() Экспорт
	Валюта = Константы.ВалютаУчета.Получить();
	
	ТестТовар	   = СозданиеТовара("ТестКлавиатура");
	ТестСклад	   = СозданиеСклада("ТестОсновной");
	ТестПокупатель = СозданиеКонтрагента("ТестПокупатель");
	
	ТестПоступлениеДенег = СозданиеПоступленияДенег(ТестПокупатель, 10000);
	ТестПриходТовара	 = СозданиеПриходаТовара(ТестТовар, ТестСклад, 10, 500);
	ТестПриходТовара.ПолучитьОбъект().Записать(РежимЗаписиДокумента.Проведение);
	
	ТестРасходТовара = СозданиеРасходаТовара(ТестТовар, ТестСклад, 5, 800);
	ОбРасходТовара = ТестРасходТовара.ПолучитьОбъект();
	ОбРасходТовара.Покупатель = ТестПокупатель;
	ОбРасходТовара.Валюта = Валюта;
	ОбРасходТовара.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
	
	ОписаниеЗапроса = ЮТЗапросы.ОписаниеЗапроса();
	ОписаниеЗапроса.ИмяТаблицы = "РегистрНакопления.ТоварныеЗапасы.Остатки";
	ОписаниеЗапроса.Условия.Добавить("Товар = &Товар");
	ОписаниеЗапроса.Условия.Добавить("Склад = &Склад");
	ОписаниеЗапроса.ЗначенияПараметров.Вставить("Товар", ТестТовар);
	ОписаниеЗапроса.ЗначенияПараметров.Вставить("Склад", ТестСклад);
	ОписаниеЗапроса.ВыбираемыеПоля.Добавить("КоличествоОстаток");
	
	ЮТест.ОжидаетЧто(ЮТЗапросы.РезультатПустой(ОписаниеЗапроса)).ЭтоЛожь();
	
	ОписаниеЗапроса = ЮТЗапросы.ОписаниеЗапроса();
	ОписаниеЗапроса.ИмяТаблицы = "РегистрНакопления.Взаиморасчеты.Остатки";
	ОписаниеЗапроса.Условия.Добавить("Контрагент = &Контрагент");
	ОписаниеЗапроса.Условия.Добавить("Валюта = &Валюта");
	ОписаниеЗапроса.ЗначенияПараметров.Вставить("Контрагент", ТестПокупатель);
	ОписаниеЗапроса.ЗначенияПараметров.Вставить("Валюта", Валюта);
	ОписаниеЗапроса.ВыбираемыеПоля.Добавить("СуммаОстаток");
	
	ЮТест.ОжидаетЧто(ЮТЗапросы.РезультатПустой(ОписаниеЗапроса)).ЭтоЛожь();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СозданиеТовара(Наименование)
	
	КонструкторТовара = ЮТест.Данные().КонструкторОбъекта("Справочник.Товары")
		.ФикцияОбязательныхПолей()
		.Установить("Вид", Перечисления.ВидыТоваров.Товар)
		.Установить("Наименование", Наименование)
		;
		
	ТоварНовый = КонструкторТовара.Записать();
	
	Возврат ТоварНовый;	
КонецФункции
	
Функция СозданиеСклада(Наименование)
	
	КонструкторСклада = ЮТест.Данные().КонструкторОбъекта("Справочник.Склады")
		.ФикцияОбязательныхПолей()
		.Установить("Наименование", Наименование)
		;
		
	СкладНовый = КонструкторСклада.Записать();
	
	Возврат СкладНовый;	
КонецФункции		

Функция СозданиеКонтрагента(Наименование)
	
	КонструкторКонтрагента = ЮТест.Данные().КонструкторОбъекта("Справочник.Контрагенты")
		.ФикцияОбязательныхПолей()
		.Установить("Наименование", Наименование)
		;
		
	КонтрагентНовый = КонструкторКонтрагента.Записать();
	
	Возврат КонтрагентНовый;	
КонецФункции

Функция СозданиеПоступленияДенег (Покупатель, Сумма)
	
	КонструкторДокумента = ЮТест.Данные().КонструкторОбъекта("Документ.ПоступлениеДенег")
		.ФикцияОбязательныхПолей()
		.Установить("Покупатель", Покупатель)
		.Установить("Валюта", Константы.ВалютаУчета.Получить())
		.Установить("Сумма", Сумма)
		;
		
	НовыйДокумент = КонструкторДокумента.Провести();
	
	Возврат НовыйДокумент;	
	
КонецФункции	

Функция СозданиеПриходаТовара(Товар, Склад, Количество, Цена)	
	
	КонструкторДокумента = ЮТест.Данные().КонструкторОбъекта("Документ.ПриходТовара")
		.ФикцияОбязательныхПолей()
		.Установить("Дата", ТекущаяДатаСеанса())
		.Установить("Склад", Склад)
		.ТабличнаяЧасть("Товары")
			.ДобавитьСтроку()
				.Установить("Товар", Товар)
				.Установить("Количество", Количество)
				.Установить("Цена", Цена)
				.Установить("Сумма", Цена * Количество)
		;
		
	НовыйДокумент = КонструкторДокумента.Записать();
	
	Возврат НовыйДокумент;
				
КонецФункции	

Функция СозданиеРасходаТовара(Товар, Склад, Количество, Цена)	
	
	КонструкторДокумента = ЮТест.Данные().КонструкторОбъекта("Документ.РасходТовара")
		.ФикцияОбязательныхПолей()
		.Установить("Дата", ТекущаяДатаСеанса())
		.Установить("Склад", Склад)
		.ТабличнаяЧасть("Товары")
			.ДобавитьСтроку()
				.Установить("Товар", Товар)
				.Установить("Количество", Количество)
				.Установить("Цена", Цена)
				.Установить("Сумма", Цена * Количество)
		;
		
	НовыйДокумент = КонструкторДокумента.Записать();
	
	Возврат НовыйДокумент;
				
КонецФункции

#КонецОбласти
